prontera,170,180,1	script	Estilista#custom_stylist	2_M_DYEINGER,{
	set .isPremium, callfunc("F_IsPremium");
	setarray .@styles[1],getbattleflag("max_cloth_color"),getbattleflag("max_hair_style"),getbattleflag("max_hair_color");
	setarray .@Look[1],7,1,6;
	mes "[Estilista]";
	mes "Seja bem-vindo(a)!";
	if (.isPremium == 1) {
		mes "Em que posso ajudá-lo?";
		set .@s, select(" ~ Cor da Roupa", " ~ Estilo de Cabelo", " ~ Cor do Cabelo");
		set .@Revert, getlook(.@Look[.@s]);
		set .@style,1;
		while(1) {
			setlook .@Look[.@s], .@style;
			message strcharinfo(PC_NAME),"Esse é o estilo #"+.@style+".";
			set .@menu$, " ~ Próximo (^0055FF"+((.@style!=.@styles[.@s])?.@style+1:1)+"^000000): ~ Anterior (^0055FF"+((.@style!=1)?.@style-1:.@styles[.@s])+"^000000): ~ Pular para...: ~ Voltar ao original (^0055FF"+.@Revert+"^000000)";
			switch(select(.@menu$)) {
				case 1: set .@style, ((.@style!=.@styles[.@s])?.@style+1:1); break;
				case 2: set .@style, ((.@style!=1)?.@style-1:.@styles[.@s]); break;
				case 3: message strcharinfo(PC_NAME),"Escolha um estilo entre 1 - "+.@styles[.@s]+".";
					input .@style,0,.@styles[.@s];
					if (!.@style) set .@style, rand(1,.@styles[.@s]);
					break;
				case 4: set .@style, .@Revert; setlook .@Look[.@s], .@Revert; break;
			}
		}
	}
	else{
		mes "Pelo visto, você ainda não é um jogador Premium.";	
		mes "\n";
		mes "Neste caso, aconselho a dar uma conversadinha com o Correspondente Premium.";
		close;
	}
}