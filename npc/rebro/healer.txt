-	script	Curandeira::Healer	4_F_ACOLYTE,{
	.NPCName$ = callfunc("P_Purple", "[Curandeira]");
	.isPremium = callfunc("F_IsPremium");
	.restTimeoutInSeconds = 180;
	.restTimeoutAsMessage$ = .restTimeoutInSeconds / 60 + " minutos";


	mes .NPCName$;
	mes "Para arrecadar fundos para " + callfunc("P_Blue", "A Sociedade da Cura de Rune-Midgard") + ", estou curando qualquer aventureiro Premium sem custo.";

	if (.isPremium) {
		if (TimeHealed > gettimetick(2)) {
			next;

			if (Sex == 0) {
				set .@referral$, "senhora";
			} else {
				set .@referral$, "senhor";
			}

			mes .NPCName$;
			mes "Mas infelizmente, " + .@referral$ + ", não consigo curar agora. Estou cansada desde a última vez que lhe curei.";
			mes "\n";
			mes "Preciso descansar por " + callfunc("P_Orange", .restTimeoutAsMessage$) + " até a próxima cura.";
			close;
		}

		next;
		mes .NPCName$;
		mes "Você precisa de mim? Preciso lhe dizer que, depois de uma cura, preciso de " + callfunc("P_Orange", .restTimeoutAsMessage$) + " para descansar antes de curar novamente.";

		switch(select("Por favor.", callfunc("P_LightGray", "Estou bem, obrigado."))) {
			case 1:
				percentheal 100, 100;
				set TimeHealed, gettimetick(2) + .restTimeoutInSeconds;
				specialeffect(EF_HEAL, AREA, getcharid(3));
				specialeffect(EF_HEALSP, AREA, getcharid(3));
				goto M_Leave;
			case 2:
				goto M_Leave;
		}
	}

	if (!.isPremium) {
		next;
		mes .NPCName$;
		mes "Eu gostaria muito te ajudar, mas vários aventureiros doentes contam com o dinheiro da Corporação Premium.";
		close;
	}
	
	close;

	M_Leave:
		next;
		mes .NPCName$;
		mes "Se cuide, aventureiro.";
		close;
}

payon,79,116,5	duplicate(Healer)	Curandeira#pay	4_F_ACOLYTE
prontera,140,174,6	duplicate(Healer)	Curandeira#prt	4_F_ACOLYTE
geffen,122,62,0	duplicate(Healer)	Curandeira#gef	4_F_ACOLYTE
morocc_mem,158,97,4	duplicate(Healer)	Curandeira#mor	4_F_ACOLYTE
alberta,25,238,5	duplicate(Healer)	Curandeira#alb	4_F_ACOLYTE
izlude,122,96,6	duplicate(Healer)	Curandeira#izl	4_F_ACOLYTE
aldebaran,136,108,5	duplicate(Healer)	Curandeira#ald	4_F_ACOLYTE
xmas,146,136,5	duplicate(Healer)	Curandeira#xma	4_F_ACOLYTE
comodo,185,153,5	duplicate(Healer)	Curandeira#com	4_F_ACOLYTE
yuno,150,187,5	duplicate(Healer)	Curandeira#yun	4_F_ACOLYTE